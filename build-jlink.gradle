plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'

    // Added for jlink task
    id 'org.beryx.jlink' version '2.25.0'
}

group = 'com.dijure.stella'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    maven { url 'https://repo.spring.io/release' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

     // Added for jlink
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

application {
    mainModule = 'com.dijure.stella'
    mainClass  = 'com.dijure.stella.StellaApplication'
}

// Lines below added for jlink
// Helpful reference: https://sergiomartinrubio.com/articles/build-a-docker-jre-image-with-java-17-and-spring/
jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--compress', '2']
    launcher {
        name = 'stella'

        jvmArgs = [
            '-Dlogback.configurationFile=./logback.xml',
            '--add-reads', 'com.dijure.stella.merged.module=com.dijure.stella'
        ]
    }

    mergedModule {
        additive = true
        uses 'ch.qos.logback.classic.spi.Configurator'

        // https://github.com/netty/netty/issues/11753
        // https://github.com/iTitus/skat-java/blob/986d29f6e69d99f7ea07f57e09daef40f3d491ce/build.gradle
        excludeProvides servicePattern: 'reactor.blockhound.integration.*'
    }

    jpackage {
        imageName = 'Stella'
        skipInstaller = true
        installerName = 'Stella'
        installerType = 'pkg'
    }

    forceMerge 'log4j-api'   // https://github.com/beryx/badass-jlink-plugin/issues/14
}
